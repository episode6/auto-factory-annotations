buildscript {
  repositories {
    jcenter()
  }
  dependencies { 
    classpath 'com.episode6.hackit.deployable:deployable:0.1.11'
    classpath 'com.episode6.hackit.gdmc:gdmc:0.1.8'
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.3'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.4"
  distributionType = "all"
}

repositories {
  jcenter()
}

group = 'com.episode6.hackit.auto.factory'
version = '1.0-SNAPSHOT'

apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.episode6.hackit.deployable.jar'
apply plugin: 'com.episode6.hackit.gdmc'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
  pomCompile
}

dependencies {
  implementation('com.google.auto.factory:auto-factory')
  pomCompile 'javax.annotation:jsr250-api'
}

shadowJar {
  classifier = ""
  dependencies {
    include(dependency('com.google.auto.factory:auto-factory'))
  }
  exclude 'com/google/auto/factory/processor/**'
  exclude 'META-INF/services/'
  exclude 'META-INF/maven/'
}

configurations.archives.artifacts.removeAll {
    it.archiveTask.is jar
}

artifacts {
  archives shadowJar
}

mavenDependencies {
  unmap "implementation"
  map "pomCompile", "compile"
}
