buildscript {
  repositories {
    jcenter()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }
  dependencies { 
    classpath 'com.episode6.hackit.deployable:deployable:0.2.0-SNAPSHOT'
    classpath 'com.episode6.hackit.gdmc:gdmc:0.1.9'
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  }
}

wrapper {
  gradleVersion = "4.9"
  distributionType = "all"
}

repositories {
  jcenter()
}

group = 'com.episode6.hackit.auto.factory'
version = '1.0-SNAPSHOT'

apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.episode6.hackit.deployable.jar'
apply plugin: 'com.episode6.hackit.gdmc'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
  pomCompile
}

dependencies {
  implementation('com.google.auto.factory:auto-factory')
  pomCompile 'javax.annotation:jsr250-api'
}

shadowJar {
  classifier = ""
  dependencies {
    include(dependency('com.google.auto.factory:auto-factory'))
  }
  exclude 'com/google/auto/factory/processor/**'
  exclude 'META-INF/services/'
  exclude 'META-INF/maven/'
}

deployable {
  publication.main {
    artifact shadowJar
  }

  pom.dependencyConfigurations {
    clear()
    map "pomCompile", "compile"
  }
}

